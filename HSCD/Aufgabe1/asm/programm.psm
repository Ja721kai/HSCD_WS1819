
		CONSTANT IO_Port,  00
        CONSTANT LED0,     01
        CONSTANT LED1,     02
        CONSTANT BNT0,     04
        
        CONSTANT COM_Port, 01
        CONSTANT TXD,      01
        CONSTANT RXD,      01
		
		CONSTANT BEGINZAHL,		30 ; ASCII Zeichen 0
		CONSTANT ENDZAHL,		3A ; ASCII Zeichen 9 + 1
		CONSTANT BEGINGROSS,	41 ; ASCII Zeichen A
		CONSTANT ENDGROSS,		5B ; ASCII Zeichen Z + 1
		CONSTANT BEGINKLEIN,	61 ; ASCII Zeichen a
		CONSTANT ENDKLEIN,  	7B ; ASCII Zeichen z + 1

; Register Mapping
; alle acht Register s0 bis s7 stehen der ISR zur Verfuegung
        NAMEREG s0, IOR
        NAMEREG s1, TMP
        NAMEREG s2, CNTH
        NAMEREG s3, CNTL
		
		NAMEREG s4, COUNTER
		NAMEREG s5, IDLE_STATE
		NAMEREG s6, READ_MODE
		NAMEREG s7, SAVED_BITS
		NAMEREG s8, DATA
		NAMEREG s9, WRITE
		NAMEREG sA, DATAW
		NAMEREG sB, COUNTERW
		NAMEREG sC, WRITE_MODE
		NAMEREG sD, WRITTEN_BITS

; alle acht Register s8 bis sF stehen der Main-Funktion zur Verfuegung

; --------------------------------------------------------------------------------------------

        ADDRESS 000
; Hauptfunktion
start:  AND    CNTH, 00   ; Initialisierung der Zaehlers ...
        AND    CNTL, 00   ; in der ISR
		OR     IDLE_STATE, 01 ; Programm ist zunächst immer idle
		AND	   READ_MODE, 00 ; initial Modus zur Startbiterkennung
		AND    SAVED_BITS, 00 ; initial keine Bits eingelesen
		AND    DATA, 00
		AND    COUNTER, 00
		AND    WRITE, 00
		AND    DATAW, 00
		AND    COUNTERW, 00
		AND    WRITE_MODE, 00
		AND    WRITTEN_BITS, 00
        ENABLE INTERRUPT
loop:   ; do nothing
        JUMP loop

; --------------------------------------------------------------------------------------------
 
; Subroutine, die den Zustand des Buttons BTN0 auf die Leuchtdiode LED0 durchschaltet
BNT0LED0:
        INPUT  IOR,  IO_Port ; IO-Port einlesen
        INPUT  TMP,  IO_Port ; IO-Port einlesen
        AND    TMP,  BNT0    ; Zustand von BNT0 ermitteln
        SR0    TMP           ; auf die Position ...
        SR0    TMP           ; ... von LED0 verschieben
        AND    IOR,  LED1    ; Zustand von LED1 ermitteln
        OR     IOR,  TMP     ; LED1 mit BNT0 konkatinieren
        OUTPUT IOR,  IO_Port ; den neuen Wert ueber IO-Port ausgeben 
        RETURN

; --------------------------------------------------------------------------------------------

; Subroutine, die die Leuchtdione LED1 mit ca. 1 Hz blinken laesst
BLINK:  ADD    CNTL, 01      ; den 16-Bit-Zaehler ... 
        ADDCY  CNTH, 00      ; .. inkrementieren
        JUMP   NC,   BLINK0  ; kein Ueberlauf -> exit, sonst
        INPUT  IOR,  IO_Port ; IO-Port einlesen
        XOR    IOR,  LED1    ; LED1 toggeln
        OUTPUT IOR,  IO_Port ; den neuen Wert ueber IO-Port ausgeben 
BLINK0: RETURN

; --------------------------------------------------------------------------------------------

; Subroutine, die Daten ueber das serielle Eingangssignal RXD abtastet,
; zu einem Byte zusammensetzt, entschidet, ob das empfangene Byte
; ein Buchstabe oder eine Ziffer ist, und ggf. das Byte seriell
; ueber das serielle Ausgangssignal TXD ausgibt
;
; in dieser Version ist eine einfache Echo-Funktion implementiert

COMM:   
        INPUT  IOR,  COM_Port ; COM-Port einlesen
		COMPARE IOR, 01		  ; Prüfen ob COM_Port == 1
		JUMP Z, CHECK_IDLE_STATE ; Prüfe ob Programm idle ist -> idle bleiben
		JUMP NZ, PROCESS_BIT  ; Eingehendes Bit verarbeiten

; --------------------------------------------------------------------------------------------

CHECK_IDLE_STATE:
		COMPARE IDLE_STATE, 01
		JUMP NZ, PROCESS_BIT
		AND COUNTER, 00 ; Falls wir im idle state sind muss der Counter immer auf 0 sein bzw. zurückgesetzt werden
		RETURN 	; ISR beenden falls Zustand idle ist

; --------------------------------------------------------------------------------------------

PROCESS_BIT:
		COMPARE READ_MODE, 01 ; Prüfe ob Startbiterkennung (0) oder Datenübernahme (1)
		JUMP Z, DATENUEBERNAHME
		JUMP NZ, STARTBITERKENNUNG

; --------------------------------------------------------------------------------------------

STARTBITERKENNUNG:
		ADD COUNTER, 01 ; Counter inkrementieren
		COMPARE COUNTER, 08 ; Überprüfe ob 8 Takte vergangen sind
		JUMP Z, ENTER_DATATRANSFER
		RETURN ; ISR beenden falls Counter noch nicht 8 erreicht hat

; --------------------------------------------------------------------------------------------

ENTER_DATATRANSFER:
		AND IDLE_STATE, 00 ; Programm wird nicht mehr als idle betrachtet
		OR READ_MODE, 01 ; Nun können wir anfangen Bits zu lesen
		AND COUNTER, 00 ; Setze Counter zurück, sodass wir diesen später auf 16 zählen können
		RETURN ; Startbit muss nicht in Datenregister zurückgeschrieben werden, da es sowieso eine 0 ist
		
; --------------------------------------------------------------------------------------------

DATENUEBERNAHME:
		ADD COUNTER, 01 ; Counter inkrementieren
		COMPARE COUNTER, 10 ; Prüfe ob 16 Takte vergangen sind
		JUMP Z, SAVE_DATA_BIT
		RETURN ; ISR beenden falls Counter noch nicht 16 erreicht hat
		
; --------------------------------------------------------------------------------------------

SAVE_DATA_BIT:
		AND COUNTER, 00
		COMPARE SAVED_BITS, 08 ; Schauen ob alle Datenbits schon da sind
		JUMP Z, CHECK_STOP_BIT
		SL0 DATA ; Platz schaffen für neues Bit
		OR DATA, IOR ; Bit eintragen in Datenspeicher
		ADD	SAVED_BITS, 01
		RETURN
		
		
; --------------------------------------------------------------------------------------------

CHECK_STOP_BIT:
		COMPARE IOR, 01  ; Prüfe ob Stopbit 1 vorliegt
		JUMP Z, CHECK_DATA ; 
		JUMP ENDROUTINE  ; Setze alle Zustände zurück auf Startzustand

; --------------------------------------------------------------------------------------------

CHECK_DATA:
		COMPARE DATA, BEGINZAHL ; Prüfe ob kleiner als ASCII 0
		JUMP C, ENDROUTINE  ; Beende Programm bei Sonderzeichen
		COMPARE DATA, ENDZAHL  ; Falls Carry -> Ziffer zwischen 0 und 9
		JUMP C, SET_WRITE_BACK	; Zahl auf TX schreiben
		COMPARE DATA, BEGINGROSS ; Prüfe ob kleiner als ASCII A
		JUMP C, ENDROUTINE
		COMPARE DATA, ENDGROSS 
		JUMP C, SET_WRITE_BACK
		COMPARE DATA, BEGINKLEIN ; Prüfe ob kleiner als ASCII a 
		JUMP C, ENDROUTINE
		COMPARE DATA, ENDKLEIN
		JUMP C, SET_WRITE_BACK

; --------------------------------------------------------------------------------------------

SET_WRITE_BACK:
		OR WRITE, 01
		AND DATAW, 00
		OR DATAW, DATA
		JUMP ENDROUTINE
		
; --------------------------------------------------------------------------------------------

ENDROUTINE:
		AND DATA, 00	; Falls Stopbit nicht vorliegt..
		AND COUNTER, 00  ; setze alles auf Startzustand
		AND SAVED_BITS, 00
		AND READ_MODE, 00
		OR IDLE_STATE, 01
		RETURN 

; --------------------------------------------------------------------------------------------

WRITE_BACK:
		COMPARE WRITE, 01
		RETURN NZ
		COMPARE WRITE_MODE, 01
		JUMP C, WRITE_STARTBIT
		JUMP Z, WRITE_DATABIT
		RETURN

; --------------------------------------------------------------------------------------------

WRITE_STARTBIT:
		AND IOR, 00
		OUTPUT IOR, COM_Port
		OR WRITE_MODE, 01
		RETURN

; --------------------------------------------------------------------------------------------
        
WRITE_DATABIT:
		ADD COUNTERW, 01
		COMPARE COUNTERW, 10
		RETURN C
		AND COUNTERW, 00
		COMPARE WRITTEN_BITS, 08
		JUMP Z, WRITE_STOPBIT
		ADD WRITTEN_BITS, 01
		SL0 DATAW
		JUMP C, WRITE_ONE
		AND IOR, 00
		OUTPUT IOR, COM_Port
		RETURN

; --------------------------------------------------------------------------------------------

WRITE_ONE:
		OR IOR, 01
		OUTPUT IOR, COM_Port
		RETURN

; --------------------------------------------------------------------------------------------

WRITE_STOPBIT:
		AND WRITTEN_BITS, 00
		AND WRITE_MODE, 00
		AND WRITE, 00
		OR IOR, 01
		OUTPUT IOR, COM_Port
		RETURN

; --------------------------------------------------------------------------------------------
		
        ADDRESS 300
; Interrupt-Service-Routine
ISR:    CALL    BNT0LED0
        CALL    BLINK
        CALL    COMM
		CALL 	WRITE_BACK
        RETURNI ENABLE

; --------------------------------------------------------------------------------------------

; Interrupt-Vektor
        ADDRESS 3FF
        JUMP ISR

